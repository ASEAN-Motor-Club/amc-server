{
  description = "AMC Game Server";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-25.05";
    flake-parts.url = "github:hercules-ci/flake-parts";
    motortown-server = {
      url = "git+https://github.com/ASEAN-Motor-Club/motortown-server-flake.git?lfs=1";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    necesse-server = {
      url = "github:ASEAN-Motor-Club/necesse-server";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    eco-server = {
      url = "github:ASEAN-Motor-Club/eco-server";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    zomboid-server = {
      url = "github:ASEAN-Motor-Club/zomboid-server";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    beammp-server = {
      url = "github:ASEAN-Motor-Club/beammp-server-flake";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    assetto-server = {
      url = "github:ASEAN-Motor-Club/assetto-server-flake";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    amc-backend = {
      url = "github:ASEAN-Motor-Club/amc-backend";
      inputs.nixpkgs.follows = "nixpkgs";
      inputs.ragenix.follows = "ragenix";
    };
    amc-peripheral = {
      url = "github:ASEAN-Motor-Club/amc-peripheral";
      inputs.nixpkgs.follows = "nixpkgs";
    };
    ragenix.url = "github:yaxitech/ragenix";
    ragenix.inputs.nixpkgs.follows = "nixpkgs";
    quadlet-nix.url = "github:SEIAROTg/quadlet-nix";
    nixpkgs-unstable.url = "github:NixOS/nixpkgs/nixos-unstable";
    opencode.url = "github:anomalyco/opencode";
    mt-pak-extract = {
      url = "github:ASEAN-Motor-Club/mt-pak-extract";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs = inputs @ {
    self,
    nixpkgs,
    nixpkgs-unstable,
    flake-parts,
    opencode,
    amc-backend,
    amc-peripheral,
    motortown-server,
    necesse-server,
    eco-server,
    zomboid-server,
    beammp-server,
    assetto-server,
    ragenix,
    quadlet-nix,
    mt-pak-extract,
    ...
  }:
    flake-parts.lib.mkFlake {inherit inputs;} {
      systems = [
        "x86_64-linux"
        "x86_64-darwin"
        "aarch64-darwin"
      ];
      flake = {
        nixosModules.gameSyslog = {
          lib,
          config,
          ...
        }: {
          options.services.gameSyslog.relpPort = lib.mkOption {
            type = lib.types.int;
            default = 2514;
            description = "RELP port for log forwarding";
          };
          config.services.rsyslogd = {
            enable = true;
            # We use mkBefore to ensure modules and templates are defined
            # before the individual services try to use them.
            extraConfig = lib.mkBefore ''
              # 1. Load Modules (Only once)
              module(load="imfile")
              module(load="omrelp")

              template(name="with_filename" type="list") {
                property(name="timestamp" dateFormat="rfc3339")
                constant(value=" ")
                property(name="hostname")
                constant(value=" ")
                property(name="syslogtag")
                constant(value=" ")
                property(name="$!metadata!filename")
                property(name="msg" spifno1stsp="on" )
                property(name="msg" droplastlf="on" )
                constant(value="\n")
              }

              Ruleset(name="mt-out") {
                action(type="omrelp"
                  target="127.0.0.1"
                  port="${toString config.services.gameSyslog.relpPort}"
                  template="with_filename"
                )
              }
            '';
          };
        };
        nixosModules.motortown-server-containers = {
          config,
          pkgs,
          lib,
          ...
        }: {
          imports = [motortown-server.nixosModules.containers];
          services.motortown-server-containers-env = {
            programs.bash.promptInit = ''
              # Set a custom prompt color
              PS1='\[\e[38;5;40m\]\u\[\e[38;5;40m\]@\h\[\e[0m\]:\W '
            '';
          };
          services.motortown-server-containers = {
            event = {
              imports = [
                self.nixosModules.gameSyslog
              ];
              motortown-server = {
                enable = false;
                enableMods = true;
                modVersion = "v12";
                enableExternalMods = {
                };
                engineIni = ''
                  mh.eventRacingMoneyPerKm=1
                  mh.eventRacingXPPerKm=20
                  mh.rentalCostRatio=0.000001
                  mh.rentalRemoveTimeSeconds=604800
                  mh.parkingTicketTimeSeconds=604800
                  mh.roadsideTowingBaseCost=10000
                  mh.aICharacterCountScale=0.0
                  mh.allowBuilding=1
                  mh.dealerVehicleRespawnTimeSeconds=10.0
                  mh.worldVehicleAbandonedParkingTicketTimeSeconds=604800.0
                  mh.worldVehicleAbandonedRespawnTimeSeconds=604800.0
                  mh.companyVehicleSlotCostBase=1
                  mh.companyEditorAsDediForCorporation=1
                '';
                enableLogStreaming = true;
                logsTag = "amc-event";
                openFirewall = true;
                port = 7779;
                queryPort = 27017;
                user = "steam";
                restartSchedule = "3000-01-01 00:00:00";
                relpServerHost = "localhost";
                environment = {
                  MOD_SERVER_PORT = "5011";
                  MOD_MANAGEMENT_PORT = "5010";
                  MOD_WEBHOOK_ENABLE_EVENTS = "none";
                };
                credentialsFile = config.age.secrets.steam.path;
                dedicatedServerConfig = {
                  ServerName = "■□■□ ASEAN Event Server ■□■□";
                  ServerMessage = ''
                    <Title>Welcome to the ASEAN Event Server</>

                    <Highlight>This server is only used for events.</>
                    If you are looking for the main server, search for the <Focus>ASEAN Motor Club</> on the server list.

                    Use <Event>/events</> to check the event schedule.

                    <Bold>Join the Discord</>
                    Visit aseanmotorclub.com to join the discord server.
                  '';
                  Password = "asean";
                  MaxPlayers = 50;
                  MaxVehiclePerPlayer = 1;
                  bAllowPlayerToJoinWithCompanyVehicles = false;
                  bAllowCompanyAIDriver = false;
                  MaxHousingPlotRentalPerPlayer = 20;
                  MaxHousingPlotRentalDays = 180;
                  HousingPlotRentalPriceRatio = 0.00001;
                  bAllowModdedVehicle = false;
                  NPCVehicleDensity = 0.0;
                  NPCPoliceDensity = 0.0;
                  bEnableHostWebAPIServer = true;
                  HostWebAPIServerPassword = "";
                  HostWebAPIServerPort = 8082;
                  Admins = [
                    {
                      UniqueNetId = "76561198378447512";
                      Nickname = "freeman";
                    }
                    {
                      UniqueNetId = "76561199496159494";
                      Nickname = "ARID";
                    }
                    {
                      UniqueNetId = "76561199174259800";
                      Nickname = "Yuuka";
                    }
                    {
                      UniqueNetId = "76561199109285302";
                      Nickname = "Nunauu";
                    }
                    {
                      UniqueNetId = "76561198093833834";
                      Nickname = "VicSay";
                    }
                    {
                      UniqueNetId = "76561198062644260";
                      Nickname = "Meehoi";
                    }
                    {
                      UniqueNetId = "76561198006148466";
                      Nickname = "dvdurL";
                    }
                    {
                      UniqueNetId = "76561198039953945";
                      Nickname = "Youyu";
                    }
                    {
                      UniqueNetId = "76561198127159716";
                      Nickname = "MeadowVick";
                    }
                    {
                      UniqueNetId = "76561198412768677";
                      Nickname = "BattleSpec";
                    }
                  ];
                };
              };
            };
          };
        };

        # Main Server
        nixosModules.motortown-server = {
          config,
          pkgs,
          lib,
          ...
        }: {
          imports = [
            motortown-server.nixosModules.default
            necesse-server.nixosModules.default
            eco-server.nixosModules.amc
            zomboid-server.nixosModules.default
            self.nixosModules.gameSyslog
          ];

          services.necesse-server = {
            enable = false;
            openFirewall = true;
            enableLogStreaming = true;
            ownerName = "freeman";
          };
          services.eco-server = {
            enable = false;
            openFirewall = true;
            enableLogStreaming = false;
            credentialsFile = config.age.secrets.ecoUserToken.path;
            discordlinkSecretFile = config.age.secrets.discordlinkBotToken.path;
          };
          services.zomboid-server = {
            enable = true;
            openFirewall = true;
            enableLogStreaming = true;
            serverName = "amc";
            adminPasswordFile = config.age.secrets.pzAdminPassword.path;
            # Native PZ Discord integration. Channels match the live config;
            # the bot token is a secret from agenix (never committed).
            # PZ restart bursts were throttling the MT server. MT is pinned to
            # cores 0-3 (motortown-server-flake), so keep PZ on everything else
            # -- a PZ map-load/restart burst can't steal MT's physical cores.
            cpuAffinity = "4 5 6 7 8 9 10 11";
            discord = {
              enable = true;
              # Channel names must match Discord EXACTLY (PZ's native filter is
              # name-string based). The real channels carry emoji prefixes, so
              # the names below include them; a bare "pz-logs" never resolves.
              chatChannel = "🎮pz-game-chat";
              logChannel = "📝pz-logs";
              commandChannel = "pz-commands";
            };
            discordTokenFile = config.age.secrets.pzDiscordToken.path;
            # Host-side SAFE workshop-update watcher (replaces the removed
            # auto-restart mod 3659447892 / ServerWorkshopModAutoRestartB42,
            # whose getCore():quit() never saved the world first -> silent
            # rollback every bounce; incident Aug 26-27). On a Steam revision
            # change it announces (Discord pzUpdateWebhook + in-game
            # servermsg), waits out the grace period, then runs fifo SAVE
            # FIRST and only then quit, so Restart=always bounces onto the
            # refreshed mods with zero world rollback.
            workshopWatcher = {
              enable = true;
              webhookFile = config.age.secrets.pzUpdateWebhook.path;
              interval = "*:0/5";
              gracePeriodMinutes = 5;
            };
            statusNotifier = {
              enable = true;
              webhookFile = config.age.secrets.pzStatusWebhook.path;
            };
            # Mod/config changelog → Discord. Posts readable entries for mod
            # additions/removals and config changes (vanilla + mod sandbox).
            # Event-driven: the deploy script fires this immediately after a
            # deploy ships a mod/config change, so it reports the moment the
            # change lands. The timer below is only a SLOW fallback safety net
            # (catches changes merged to zomboid-server master but not shipped
            # through our deploy flow, e.g. a direct push) — kept slow to stay
            # well under the unauthenticated GitHub API limit (60 req/hr).
            changelogNotifier = {
              enable = true;
              webhookFile = config.age.secrets.pzChangelogWebhook.path;
              interval = "*:0";   # hourly fallback only; deploy is the primary trigger
            };
          };
          services.motortown-server = {
            enable = true;
            enableMods = true;
            enableLogStreaming = true;
            restartSchedule = "*-*-* 08:30:00";
            restartAnnouncementSchedule = "*-*-* 08:15,25,29";
            #modVersion = "server-v0.40.0-rc8";
            modVersion = (import ./mod-versions.nix).main;
            enableExternalMods = {
              # 7.19 mods. 2 mods without 7.19-compatible paks disabled:
              #   qxZap_CranyUnlocked_P (old pak crashes 7.19), Schedule_I_v0.4.8_0.7.18+1_P
              "MajasDetailWorksV3.3-7.19-SERVER_P" = true;
              "MajasMnTrailerworksV7-7.19_P" = true;
              "qxZap_satigt3_MoreAttachments_P" = true;
            };
            engineIni = ''
              mh.maxCombinedVehicleLength=20000
              mh.fuelPriceByInventoryMax=3
              mh.housingValidateHousingArea=0
              mh.housingMaxBuildingPerHouse=120
              mh.invalidPartsDisableLeaderboard=0
              mh.refuelEVMinPercentPerSeconds=0.002000000026077032
              mh.refuelkWPerSeconds=0.2
              mh.trafficSpawnVehicleMaxDistance=40000.0
              mh.trafficSpawnVehicleMinDistance=25000.0
              mh.towPaymentMultiplier=3
              mh.vehicleMaxGVWFinePerTon=1
              mh.vehicleMaxWeightFine=1
              mh.vehicleMaxGVWKg=80000
              mh.deliveryOnlineAccessCostPerHour=1
              mh.eventRacingMoneyPerKm=200
              mh.busPaymentMultiplier=5
              mh.garbageCollectRateDecreasePerSeconds=0.00001
              mk.policeSpikePadDeploySeconds=6
              mk.policeSpikePadDurationSeconds=60
            '';
            openFirewall = true;
            user = "steam";
            credentialsFile = config.age.secrets.steam.path;
            discordWebhookEnvironmentFile = config.age.secrets.backend.path;
            relpServerHost = "localhost";
            environment = {
              MOD_SERVER_PORT = "5001";
              MOD_MANAGEMENT_PORT = "5000";
              MOD_WEBHOOK_ENABLE_EVENTS = "none";
            };
            dedicatedServerConfig = {
              # ServerName = lib.mkDefault "Vanilla+ | ASEAN Motor Club | discord.gg/aseanmotorclub";
              # ServerName = "〈 ASEAN Motor Club 〉 discord.gg/aseanmotorclub";
              ServerName = "★★ ASEAN Motor Club ★★  discord.gg/aseanmotorclub";
              ServerMessage = ''                <Title>ASEAN Motor Club</>
                <Small>Welcome | 你好 | Selamat Datang | Sawasdee Krub | Maligayang Pagdating</>

                <Bold>Slash Commands</>
                Type <Highlight>/help</> to see all available commands, and try them out!
                These custom features are unique to our server.

                <Bold>Read The Rules</>
                - Do not abandon your vehicle and block traffic,
                - Street racing is allowed, but please apologise if you crash into someone,
                - See the discord for all the rules.

                <Bold>Server Settings</>
                - Mods (optional): Road Trains, Maja's Detail Works
                - AI enabled. Max vehicles per player: ${toString config.services.motortown-server.dedicatedServerConfig.MaxVehiclePerPlayer}.
                - Rent lasts ${toString config.services.motortown-server.dedicatedServerConfig.MaxHousingPlotRentalDays} days

                <Bold>Radio</>
                Tune in to our very own Radio ASEAN station!
                www.aseanmotorclub.com/radio
                Submit song requests by using the <Highlight>/song_request</> command.

                <Bold>About ASEAN</>
                The Association of Southeast Asian Nations is made up of 10 Southeast Asian countries:
                Indonesia, Philippines, Vietnam, Thailand, Myanmar, Malaysia, Cambodia, Laos, Singapore, Brunei Darussalam.
              '';
              Password = "";
              MaxPlayers = 30;
              MaxVehiclePerPlayer = 16;
              bAllowPlayerToJoinWithCompanyVehicles = true;
              bAllowAdminToRemoveAdmin = true;
              bAllowCompanyAIDriver = true;
              bAllowCorporation = false;
              MaxHousingPlotRentalPerPlayer = 20;
              MaxHousingPlotRentalDays = 15;
              HousingPlotRentalPriceRatio = 1.0;
              bAllowModdedVehicle = true;
              NPCVehicleDensity = 0.5;
              NPCPoliceDensity = 0.0;
              bEnableHostWebAPIServer = true;
              HostWebAPIServerPassword = "";
              HostWebAPIServerPort = lib.mkDefault 8080;
              Admins = [
                {
                  UniqueNetId = "76561198378447512";
                  Nickname = "freeman";
                }
              ];
            };
          };

          networking.firewall.interfaces."tailscale0".allowedTCPPorts = lib.mkIf config.services.tailscale.enable [
            config.services.motortown-server.dedicatedServerConfig.HostWebAPIServerPort
            (lib.strings.toInt config.services.motortown-server.environment.MOD_SERVER_PORT)
          ];
        };

        nixosModules.beammp-server = {
          config,
          pkgs,
          lib,
          ...
        }: {
          imports = [beammp-server.nixosModules.default];
          services.beammp-server = {
            enable = false; # disabled during Project Zomboid burn-in
            openFirewall = true;
            name = "★★ ASEAN Motor Club ★★ | BeamNG.drive";
            description = "ASEAN Motor Club BeamMP Server - discord.gg/aseanmotorclub";
            tags = "Freeroam,Modded,lang:english";
            maxPlayers = 20;
            maxCars = 100;
            map = "/levels/west_coast_usa/info.json";
            isPrivate = false;
            allowGuests = true;
            restartSchedule = "*-*-* 09:00:00";
            authKeyFile = config.age.secrets.beammp-auth.path;
            discordWebhookEnvironmentFile = config.age.secrets.backend.path;
            enableCareerMP = true;
            careerMPVersion = "v0.0.37";
            enableRLS = false;
            enableRiverHighway = false;
            rlsCompatPatchVersion = "v1.0.0-beta.16";
            memoryMax = "4096M";
            memoryHigh = "3072M";
            careerMPConfig = {
              server = {
                allowTransactions = true;
              };
              client = {
                # Use a fixed save name so progression survives BeamMP account system outages.
                # With serverSaveNameEnabled = true, CareerMP loads `serverSaveName` for every
                # player instead of keying on (unstable) BeamMP nicknames.
                serverSaveName = "BeamMP";
                serverSaveNameEnabled = true;
                roadTrafficEnabled = true;
                roadTrafficAmount = 10;
                extraTrafficEnabled = true;
                extraTrafficAmount = 5;
                parkedTrafficEnabled = true;
                parkedTrafficAmount = 5;
              };
            };
          };
        };

        nixosModules.assetto-server = {
          config,
          pkgs,
          lib,
          ...
        }: {
          imports = [assetto-server.nixosModules.default];
          services.assetto-server = {
            enable = true;
            openFirewall = true;
            serverName = "★★ ASEAN Motor Club ★★ | Assetto Corsa Freeroam";
            track = "shuto_revival_project_beta";
            maxPlayers = 30;
            enableAi = true;
            aiTrafficSlots = 170;
            password = "";
            isPrivate = false;
            contentHostPath = "/var/lib/ac-content";
            cpuAffinity = "6 7";
            memoryMax = "1G";
            restartSchedule = "*-*-* 09:30:00";
          };
        };

        nixosConfigurations.asean-mt-server = nixpkgs.lib.nixosSystem {
          modules = [
            ./machines/asean-mt-server/configuration.nix
            ragenix.nixosModules.default

            ({
              config,
              lib,
              ...
            }: {
              imports = [
                ragenix.nixosModules.default
              ];
              age.secrets.steam = {
                file = ./secrets/steam.age;
                mode = "400";
                owner = "steam";
              };
              age.secrets.tailscale = {
                file = ./secrets/tailscale.age;
                mode = "400";
              };
              age.secrets.ecoUserToken = {
                file = ./secrets/ecoUserToken.age;
                mode = "400";
                owner = "steam";
              };
              age.secrets.discordlinkBotToken = {
                file = ./secrets/discordlink-bot-token.age;
                mode = "400";
                owner = "steam";
              };

              age.secrets.backend = {
                file = ./secrets/backend.age;
                mode = "400";
              };
              age.secrets.beammp-auth = {
                file = ./secrets/beammp-auth.age;
                mode = "400";
                # owner only exists when the beammp module is enabled and creates
                # the 'beammp' user; omit the owner (falls back to root) when
                # beammp is disabled so agenix chown doesn't fail on a missing user.
                owner = lib.mkIf config.services.beammp-server.enable "beammp";
              };
              age.secrets.pzAdminPassword = {
                file = ./secrets/pz-admin-password.age;
                mode = "400";
                owner = "steam";
              };
              # PZ Discord bot token. Only present once PR #39 (the secret) is
              # merged; the .age file is committed, the plaintext never is.
              age.secrets.pzDiscordToken = {
                file = ./secrets/pz-discord-token.age;
                mode = "400";
                owner = "steam";
              };
              age.secrets.pzUpdateWebhook = {
                file = ./secrets/pz-update-webhook.age;
                mode = "400";
              };
              age.secrets.pzStatusWebhook = {
                file = ./secrets/pz-status-webhook.age;
                mode = "400";
              };
              age.secrets.pzChangelogWebhook = {
                file = ./secrets/pz-changelog-webhook.age;
                mode = "400";
              };
            })

            self.nixosModules.motortown-server
            self.nixosModules.motortown-server-containers
            self.nixosModules.beammp-server
            # self.nixosModules.assetto-server  # disabled for now
            ({
              config,
              pkgs,
              lib,
              ...
            }: let
              # === File-based Update Bridge ===
              # Backend writes a trigger file → host systemd .path unit watches → starts the update service.
              updateTriggerDir = "/var/lib/motortown-update-trigger";

              updateScript = pkgs.writeShellScriptBin "update-motortown" ''
                echo "update requested at $(date)" > ${updateTriggerDir}/trigger
              '';
            in {
              imports = [
                amc-backend.nixosModules.backend
                amc-backend.nixosModules.log-listener
                (import ./nix/db_backup.nix)
              ];

              # GeoDjango native library paths
              environment.variables = {
                GEOS_LIBRARY_PATH = "${pkgs.geos}/lib/libgeos_c.so";
                GDAL_LIBRARY_PATH = "${pkgs.gdal}/lib/libgdal.so";
              };

              # Allow unfree for timescaledb
              nixpkgs.config.allowUnfree = true;

              # --- amc-backend service (production) ---
              services.amc-backend = {
                enable = true;
                port = 9000;
                allowedHosts = [
                  "api.aseanmotorclub.com"
                  "localhost"
                  "127.0.0.1"
                  "server.aseanmotorclub.com"
                  "www.aseanmotorclub.com"
                  "admin.aseanmotorclub.com"
                  "asean-mt-server"
                ];
                environmentFile = config.age.secrets.backend.path;
                environment = {
                  MOD_SERVER_API_URL = "http://localhost:5001";
                  GAME_SERVER_API_URL = "http://localhost:8080";
                  EVENT_GAME_SERVER_API_URL = "http://127.0.0.1:8082";
                  EVENT_MOD_SERVER_API_URL = "http://localhost:5011";
                  MOD_MANAGEMENT_API_URL = "http://localhost:5000";
                  EVENT_MOD_MANAGEMENT_API_URL = "http://localhost:5010";
                  UPDATE_MOTORTOWN_SCRIPT = "${updateScript}/bin/update-motortown";
                  PARTY_BONUS_ENABLED = "1";
                  WEBHOOK_SSE_ENABLED = "1";
                  DISCORD_CRIMINAL_STATS_CHANNEL_ID = "1486645816042061864";
                  DISCORD_COP_STATS_CHANNEL_ID = "1486645931595272222";
                  DISCORD_BEAMMP_STATUS_CHANNEL_ID = "1504133800425291930";
                  # Role gate for the event-admin Discord commands (join/kick to event).
                  # Read by amc-backend PR #79 (DISCORD_EVENT_ADMIN_ROLE_ID).
                  DISCORD_EVENT_ADMIN_ROLE_ID = "1395460420189421713";
                  BEAMMP_SERVER_HOST = "127.0.0.1";
                  BEAMMP_SERVER_PORT = "30814";
                  # --- Automated player-name moderation (auto-rename) ---
                  # Key (OPENAI_API_KEY_OPENROUTER) is injected via the backend age secret.
                  NAMER_ENABLED = "1";
                  NAMER_LLM_MODEL = "openai/gpt-5.6-luna";
                  NAMER_AUTO_CONFIDENCE_THRESHOLD = "0.9";
                  NAMER_REVIEW_CHANNEL_ID = "1366478091131551834";
                  NAMER_ANNOUNCE = "1";
                };
              };

              # Point PostgreSQL at the data directory from Phase 1 bind mount
              services.postgresql.dataDir = "/var/lib/amc-postgresql/16";

              # --- Log listener (rsyslogd + RELP) ---
              services.amc-log-listener = {
                enable = true;
                relpPort = 2514;
              };

              # --- nginx vhost for api.aseanmotorclub.com ---
              services.nginx.virtualHosts."api.aseanmotorclub.com" = {
                enableACME = true;
                forceSSL = true;
                locations = {
                  "/" = {
                    proxyPass = "http://127.0.0.1:9000/api/";
                    recommendedProxySettings = true;
                    extraConfig = ''
                      add_header 'Access-Control-Allow-Origin' '*' always;
                      add_header 'Access-Control-Allow-Methods' 'POST, PUT, DELETE, GET, PATCH, OPTIONS' always;
                    '';
                  };
                  "/api" = {
                    proxyPass = "http://127.0.0.1:9000";
                    recommendedProxySettings = true;
                    extraConfig = ''
                      add_header 'Access-Control-Allow-Origin' '*' always;
                      add_header 'Access-Control-Allow-Methods' 'POST, PUT, DELETE, GET, PATCH, OPTIONS' always;
                    '';
                  };
                  "/admin" = {
                    proxyPass = "http://127.0.0.1:9000";
                    recommendedProxySettings = true;
                  };
                  "/static/" = let
                    inherit (amc-backend.packages.${pkgs.system}) staticRoot;
                  in {
                    alias = "${staticRoot}/";
                  };
                  # DokuWiki OAuth endpoints
                  "/o/" = {
                    proxyPass = "http://127.0.0.1:9000";
                    recommendedProxySettings = true;
                  };
                };
              };

              # Expose RELP + PostgreSQL on tailscale interface
              networking.firewall.interfaces."tailscale0".allowedTCPPorts = lib.mkIf config.services.tailscale.enable [
                2514 # RELP log listener
                5432 # PostgreSQL (bot read access)
              ];

              systemd.tmpfiles.rules = [
                "d /var/lib/amc-postgresql 0750 postgres postgres -"
                "d ${updateTriggerDir} 0777 root root -"
                "d /var/lib/amc/error-reports 0755 amc amc -"
                "e /var/lib/amc/error-reports/*.html - - - 30d"
              ];

              # === File-based Update Bridge ===
              # Recovery service: restarts the game server if the update service fails
              systemd.services.motortown-update-recovery = {
                description = "Restart motortown-server after failed update";
                serviceConfig = {
                  Type = "oneshot";
                };
                script = ''
                  systemctl start motortown-server.service
                '';
              };

              # Wire failure recovery to the update service
              systemd.services.motortown-server-update = {
                onFailure = ["motortown-update-recovery.service"];
              };

              # Host-side: watch for trigger file and start the update service
              systemd.paths.motortown-update-trigger = {
                description = "Watch for update trigger from backend";
                wantedBy = ["multi-user.target"];
                pathConfig = {
                  PathChanged = "${updateTriggerDir}/trigger";
                  Unit = "motortown-update-triggered.service";
                };
              };

              # Triggered by the .path unit — starts the existing motortown-server-update service
              systemd.services.motortown-update-triggered = {
                description = "Update motortown-server (triggered from backend)";
                serviceConfig = {
                  Type = "oneshot";
                };
                script = ''
                  rm -f ${updateTriggerDir}/trigger
                  systemctl start motortown-server-update.service
                '';
              };
            })
          ];
        };

        nixosConfigurations.amc-peripheral = nixpkgs.lib.nixosSystem {
          modules = [
            ./machines/amc-peripheral/configuration.nix
            ragenix.nixosModules.default
            amc-peripheral.nixosModules.default
            ./machines/amc-peripheral/hermes.nix

            # Make mt-pak-extract flake available to modules
            {_module.args.mt-pak-extract = mt-pak-extract;}

            # Use opencode from the official flake — nixpkgs versions are too old.
            # Mark prettier as external so bun's bundler skips resolving it.
            # Only the dev-only `generate` command imports prettier; `serve` doesn't.
            ({pkgs, ...}: {
              nixpkgs.overlays = [
                (final: prev: {
                  opencode = (opencode.packages.${prev.system}.default).overrideAttrs (old: {
                    postPatch =
                      (old.postPatch or "")
                      + ''
                        substituteInPlace packages/opencode/script/build.ts \
                          --replace-fail 'external: ["node-gyp"]' \
                            'external: ["node-gyp", "prettier", "prettier/plugins/babel", "prettier/plugins/estree"]'
                      '';
                  });
                })
              ];
            })

            ({
              config,
              pkgs,
              ...
            }: {
              age.secrets.peripheral-bots = {
                file = ./secrets/peripheral-bots.age;
                mode = "400";
              };
              age.secrets.cookies = {
                file = ./secrets/cookies.age;
                mode = "400";
              };
              age.secrets.opencode-peripheral = {
                file = ./secrets/opencode-peripheral.age;
                mode = "400";
                owner = "opencode";
              };
              age.secrets.oauth2-proxy-peripheral = {
                file = ./secrets/oauth2-proxy-peripheral.age;
                mode = "400";
              };
              age.secrets.coding-agent-app-key = {
                file = ./secrets/coding-agent-app-key.age;
                owner = "opencode";
                group = "root";
                mode = "440";
              };

              # Allow Nix daemon to fetch GitHub repos (rate limit avoidance + private repos).
              # Token is generated by a systemd timer using the GitHub App (see configuration.nix).
              nix.extraOptions = ''
                !include /etc/nix/github-access-tokens.conf
              '';

              # Bootstrap: write an empty file so nix.conf !include doesn't fail on first boot
              system.activationScripts.nix-github-tokens = ''
                mkdir -p /etc/nix
                [ -f /etc/nix/github-access-tokens.conf ] || echo "# awaiting token refresh" > /etc/nix/github-access-tokens.conf
              '';

              services.amc-peripheral = {
                enable = true;
                environmentFile = config.age.secrets.peripheral-bots.path;
                cookiesPath = config.age.secrets.cookies.path;
                dbPath = "/var/lib/radio/radio.db";
                icecast.admin.password = "aseanmotorclub1234";
                sharry = {
                  enable = true;
                };
              };

              services.github-runners."amc-peripheral-deploy" = {
                enable = true;
                replace = true;
                url = "https://github.com/ASEAN-Motor-Club/amc-server";
                name = "amc-peripheral";
                tokenFile = "/var/lib/github-runner-token/token";
                user = "root";
                package = nixpkgs-unstable.legacyPackages.${pkgs.system}.github-runner;
                extraLabels = ["deploy-peripheral" "nix"];
                extraPackages = with pkgs; [nix git openssh nixos-rebuild gh];
                serviceOverrides = {
                  ProtectHome = "none";
                  ProtectSystem = "false";
                };
              };
            })

            # === Staging test server (migrated from asean-mt-server container) ===
            motortown-server.nixosModules.default
            self.nixosModules.gameSyslog

            ({
              config,
              pkgs,
              lib,
              ...
            }: {
              imports = [
                amc-backend.nixosModules.backend
                amc-backend.nixosModules.log-listener
              ];

              age.secrets.backend-staging = {
                file = ./secrets/backend-staging.age;
                mode = "400";
              };
              age.secrets.steam-game = {
                file = ./secrets/steam.age;
                mode = "400";
                owner = "steam";
              };

              environment.variables = {
                GEOS_LIBRARY_PATH = "${pkgs.geos}/lib/libgeos_c.so";
                GDAL_LIBRARY_PATH = "${pkgs.gdal}/lib/libgdal.so";
              };

              nixpkgs.config.allowUnfree = true;

              users.users.steam = {
                isNormalUser = true;
                home = "/home/steam";
                createHome = true;
                shell = pkgs.bash;
                extraGroups = ["modders"];
              };

              services.gameSyslog.relpPort = 2515;

              services.amc-backend = {
                enable = true;
                port = 9001;
                allowedHosts = [
                  "test.aseanmotorclub.com"
                  "localhost"
                  "127.0.0.1"
                ];
                environmentFile = config.age.secrets.backend-staging.path;
                environment = {
                  GAME_SERVER_API_URL = "http://localhost:8081";
                  MOD_SERVER_API_URL = "http://localhost:5001";
                  WEBHOOK_SERVER_API_URL = "http://localhost:5000";
                  MOD_MANAGEMENT_API_URL = "http://localhost:5000";
                  PARTY_BONUS_ENABLED = "1";
                  WEBHOOK_SSE_ENABLED = "1";
                  CHAT_VIA_WEBHOOK = "1";
                  CACHE_KEY_PREFIX = "test_";
                  IS_TEST_SERVER = "1";
                  GAME_LOG_TIMEZONE = "UTC";
                };
              };

              services.amc-log-listener = {
                enable = true;
                relpPort = 2515;
              };

              systemd.services.syslog.serviceConfig.TimeoutStopSec = "5s";

              services.motortown-server = {
                enable = true;
                enableMods = true;
                maxFps = 30;
                restartSchedule = "3000-01-01 00:00:00";
                betaBranch = "beta";
                modVersion = (import ./mod-versions.nix).staging;
                enableExternalMods = {
                  CarPartsImport_P = false;
                  MoneyRun_P = false;
                  qxZap_CranyUnlocked_P = false;
                  "MajasDetailWorksV3-7.18_P" = false;
                  "MajasMnTrailerworksV6-7.18_P" = false;
                  # 0.7.18-era pak crashes the 0.7.19 server at boot with
                  # status=3/NOTIMPLEMENTED (UE5 asset mismatch during engine
                  # init). Disabled 2026-08-15 (restart loop, 14 restarts on
                  # 2026-08-30 attempt); a 0.7.19-compatible rebuild
                  # (Schedule_I_v0.4.6_0.7.19_P.pak) exists in
                  # mt-pak-extract-wt-0719/mods/schedule-i/builds/ but is not
                  # yet uploaded/configured. Re-enable only with that pak.
                  "Schedule_I_v0.4.8_0.7.18+1_P" = false;
                  qxZap_satigt3_MoreAttachments_P = true;
                };
                engineIni = ''
                  mh.maxCombinedVehicleLength=4000
                  mh.cargoStackMaxVehicleHeight=420
                  mh.eventRacingMoneyPerKm=300
                  mh.fuelPriceByInventoryMax=5
                  mh.housingValidateHousingArea=0
                  mh.invalidPartsDisableLeaderboard=0
                  mh.cargoPaymentMultiplier=10
                  mh.refuelEVMinPercentPerSeconds=0.002000000026077032
                  mh.refuelkWPerSeconds=0.5
                  mh.trafficSpawnVehicleMaxDistance=20000.0
                  mh.trafficSpawnVehicleMinDistance=10000.0
                  mh.fuelPriceByInventoryMax=10.0
                  mh.fuelPriceByInventoryMin=10.0
                  mh.aiVehicleMaxDistance=50000.0
                  mh.trafficAgentMaxTickDeltaSeconds=5.0
                  mh.trafficTickPerFrame=20.0
                  mh.companyVehicleSlotCostBase=1
                  mh.companyEditorAsDediForCorporation=1
                '';
                enableLogStreaming = true;
                logsTag = "amc-test";
                openFirewall = true;
                port = 27778;
                queryPort = 27016;
                user = "steam";
                relpServerHost = "localhost";
                relpServerPort = 2515;
                environment = {
                  MOD_SERVER_PORT = "5001";
                  MOD_MANAGEMENT_PORT = "5000";
                  MOD_WEBHOOK_ENABLE_EVENTS = "none";
                  MOD_AUTO_INJECT_DP = "1";
                  MOD_INJECT_DP_LOC_X = "-290000";
                  MOD_INJECT_DP_LOC_Y = "190000";
                  MOD_INJECT_DP_LOC_Z = "-21900";
                  MOD_INJECT_DP_NAME = "Maize Farm";
                };
                credentialsFile = config.age.secrets.steam-game.path;
                dedicatedServerConfig = {
                  ServerName = "ASEAN Test 2";
                  ServerMessage = ''                    THIS IS A TEST SERVER.
                    Please join ASEAN Motor Club instead'';
                  Password = "";
                  MaxPlayers = 50;
                  MaxVehiclePerPlayer = 10;
                  bAllowPlayerToJoinWithCompanyVehicles = true;
                  bAllowCompanyAIDriver = true;
                  MaxHousingPlotRentalPerPlayer = 20;
                  MaxHousingPlotRentalDays = 15;
                  HousingPlotRentalPriceRatio = 5.0;
                  bAllowModdedVehicle = true;
                  NPCVehicleDensity = 0.01;
                  NPCPoliceDensity = 0.0;
                  bEnableHostWebAPIServer = true;
                  HostWebAPIServerPassword = "";
                  HostWebAPIServerPort = 8081;
                  Admins = [
                    {
                      UniqueNetId = "76561198378447512";
                      Nickname = "freeman";
                    }
                  ];
                };
              };

              # Use default CPUAffinity from motortown-server.nix ("0 1 2 3")
              # to isolate game server from backend services on shared host.

              networking.firewall.allowedTCPPorts = [
                9001
                8081
                5001
              ];

              services.nginx.virtualHosts."test.aseanmotorclub.com" = {
                enableACME = true;
                forceSSL = true;
                locations = {
                  "/" = {
                    proxyPass = "http://127.0.0.1:9001/api/";
                    recommendedProxySettings = true;
                    extraConfig = ''
                      add_header 'Access-Control-Allow-Origin' '*' always;
                      add_header 'Access-Control-Allow-Methods' 'POST, PUT, DELETE, GET, PATCH, OPTIONS' always;
                    '';
                  };
                  "/api" = {
                    proxyPass = "http://127.0.0.1:9001";
                    recommendedProxySettings = true;
                    extraConfig = ''
                      add_header 'Access-Control-Allow-Origin' '*' always;
                      add_header 'Access-Control-Allow-Methods' 'POST, PUT, DELETE, GET, PATCH, OPTIONS' always;
                    '';
                  };
                  "/admin" = {
                    proxyPass = "http://127.0.0.1:9001";
                    recommendedProxySettings = true;
                  };
                  "/static/" = let
                    inherit (amc-backend.packages.${pkgs.system}) staticRoot;
                  in {
                    alias = "${staticRoot}/";
                  };
                };
              };
            })
          ];
        };
      };
      perSystem = {
        config,
        self',
        inputs',
        pkgs,
        system,
        ...
      }: let
        pkgs = nixpkgs.legacyPackages.${system};
      in {
        devShells.default = pkgs.mkShell {
          packages =
            [
              pkgs.nil
              pkgs.alejandra
              pkgs.nixos-rebuild
              pkgs.google-cloud-sdk
              pkgs.ffmpeg
              pkgs.rustc
              pkgs.cargo
              pkgs.jq
              pkgs.rsync
              pkgs.gh
            ]
            ++ (import ./nix/scripts.nix {
              lib = nixpkgs.lib;
              inherit pkgs;
              argc = pkgs.argc;
            });
          buildInputs = [
            (ragenix.packages.${system}.default)
          ];
        };
      };
    };
}
